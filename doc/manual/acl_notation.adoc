//
// The authors of this file have waived all copyright and
// related or neighboring rights to the extent permitted by
// law as described by the CC0 1.0 Universal Public Domain
// Dedication. You should have received a copy of the full
// dedication along with this file, typically as a file
// named <CC0-1.0.txt>. If not, it may be available at
// <https://creativecommons.org/publicdomain/zero/1.0/>.
//

Macro parameters are generally notated with one or more pairs of `<>`
brackets, where stem:[n] pairs of brackets means the parameter will be
expanded exactly stem:[n - 1] times during rescanning after the macro
call is expanded.
In other words, the argument written in the call will be expanded
exactly stem:[n] times: once during argument collection, and
stem:[n - 1] times during rescanning.

.{empty}
[example]
====
.`configure.ac`
[source]
----
AC_INIT([[example]], 1)
AM_INIT_AUTOMAKE([foreign])

include::partials/acl_include_gatbps_ac_fragment.adoc[]

m4_pushdef([f1], [[$1]])          dnl would be notated as f1(<x>)
m4_pushdef([f2], [$1])            dnl would be notated as f2(<<x>>)
m4_pushdef([f3], [m4_expand($1)]) dnl would be notated as f3(<<<x>>>)

m4_pushdef([x], [y])

[printf '%s\n'] 'f1(x)';       dnl prints y
[printf '%s\n'] 'f2(x)';       dnl prints y
[printf '%s\n'] 'f3(x)';       dnl prints y

[printf '%s\n'] 'f1([x])';     dnl prints x
[printf '%s\n'] 'f2([x])';     dnl prints y
[printf '%s\n'] 'f3([x])';     dnl prints y

[printf '%s\n'] 'f1([[x]])';   dnl prints [x]
[printf '%s\n'] 'f2([[x]])';   dnl prints x
[printf '%s\n'] 'f3([[x]])';   dnl prints y

[printf '%s\n'] 'f1([[[x]]])'; dnl prints [[x]]
[printf '%s\n'] 'f2([[[x]]])'; dnl prints [x]
[printf '%s\n'] 'f3([[[x]]])'; dnl prints x

m4_popdef([x])

m4_popdef([f3])
m4_popdef([f2])
m4_popdef([f1])

m4_syscmd([>Makefile.am])
AC_CONFIG_FILES([Makefile])
AC_OUTPUT
----

.Output
[source]
----
$ autoreconf -i -f && ./configure
...
y
y
y
x
y
y
[x]
x
y
[[x]]
[x]
x
...
----
====

Optional macro parameters are notated with a trailing `?` character and
are always the trailing parameters of the macro.
If an optional parameter is given, all optional parameters to its left
must also be given.

//
